buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'

// ~~~ things can and will go wrong if you edit the build script ~~~ //

version = version_major + '.' + version_minor + '.' + version_patch
group = "cad97.spawnercraftkt"
archivesBaseName = "spawnercraftkt"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
compileKotlin {
    kotlinOptions {
        jvmTarget = targetCompatibility
        freeCompilerArgs += "-Xskip-runtime-version-check"
    }
}

minecraft {
    version = mc_version + '-' + forge_version
    runDir = "run"
    mappings = mcp_mapping.toString()
    makeObfSourceJar = false
}

repositories {
    // Forgelin
    jcenter()
    maven {
        name "shadowfacts"
        url "http://maven.shadowfacts.net/"
    }
    // JEI
    maven {
        name = "Progwml6"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
}

dependencies {
    deobfCompile group: "net.shadowfacts", name: "Forgelin", version: "1.5.0"
    deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "minecraft.version", project.minecraft.version

    // files with expand tokens
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info', 'SpawnerCraft.kt'

        expand project.properties
    }

    // copy everything else
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
