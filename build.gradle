buildscript {
    ext.kotlin_version = '1.1.3'
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'

// ~~~ things can and will go wrong if you edit the build script ~~~ //

version = version_major + '.' + version_minor + '.' + version_patch
group = "cad97.spawnercraftkt"
archivesBaseName = "spawnercraftkt"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = mc_version + '-' + forge_version
    runDir = "run"
    mappings = mcp_mapping.toString()
    makeObfSourceJar = false
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "minecraft.version", project.minecraft.version

    // files with expand tokens
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info', 'SpawnerCraft.kt'

        expand project.properties
    }
        
    // copy everything else
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    // shading
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

reobf {
    jar {
        // kotlin shading
        extraLines += [ "PK: kotlin cad97/spawnercraftkt/repack/kotlin" ]
        extraLines += [ "PK: org/jetbrains/annotations cad97/spawnercraftkt/repack/annotations" ]
        extraLines += [ "PK: org/intellij/lang/annotations cad97/spawnercraftkt/repack/lang/annotations"]
    }
}
